#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(_, __)
	ЭтотОбъект.ДатаНачалаДействияЦены = НачалоДня(ТекущаяДатаСеанса());
	ЭтотОбъект.Номенклатура = Параметры.Номенклатура;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ЗаписатьНаСервере()
	наборЗаписейРегистра_ = РегистрыСведений.ЦеныНоменклатуры.СоздатьНаборЗаписей();
	наборЗаписейРегистра_.Отбор.Номенклатура.Установить(Номенклатура);
	наборЗаписейРегистра_.Отбор.ВидЦены.Установить(ВидЦены);
	наборЗаписейРегистра_.Отбор.Период.Установить(ДатаНачалаДействияЦены);

	наборЗаписейРегистра_.Прочитать();
	Если наборЗаписейРегистра_.Количество() Тогда
		наборЗаписейРегистра_[0].Цена = Цена;
	Иначе
		новаяЗаписьЦены_ = наборЗаписейРегистра_.Добавить();
		новаяЗаписьЦены_.Номенклатура = Номенклатура;
		новаяЗаписьЦены_.ВидЦены = ВидЦены;
		новаяЗаписьЦены_.Период = ДатаНачалаДействияЦены;
		новаяЗаписьЦены_.Цена = Цена;
	КонецЕсли;

	наборЗаписейРегистра_.Записать();
КонецПроцедуры

&НаКлиенте
Процедура Записать(_)
	ценаНаДату_ = РаботаСЦенамиВызовСервера.ПолучитьЦенуНаКонкретнуюДату(
			ЭтотОбъект.Номенклатура, ЭтотОбъект.ВидЦены, ЭтотОбъект.ДатаНачалаДействияЦены);
	Если ценаНаДату_ <> Неопределено Тогда

		текстВопроса_ = СтрШаблон("На указанную дату уже имеется введенная цена %1, вы действительно хотите изменить цену?", ценаНаДату_);
		оповещение_ = Новый ОписаниеОповещения("_послеОтветаНаВопросОбИзмененииЦены_ОВ", ЭтотОбъект);
		ПоказатьВопрос(оповещение_, текстВопроса_, РежимДиалогаВопрос.ДаНет, , , "Внимание!");
	Иначе
		ЗаписатьНаСервере();
		Закрыть();

	КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура _послеОтветаНаВопросОбИзмененииЦены_ОВ(результатДиалога, __) Экспорт
	Если результатДиалога = КодВозвратаДиалога.Да Тогда
		ЗаписатьНаСервере();
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
