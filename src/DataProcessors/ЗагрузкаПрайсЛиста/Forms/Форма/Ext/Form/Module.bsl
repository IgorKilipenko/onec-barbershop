#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(_, __)
    Элементы.ПоместитьВДокумент.Видимость = Ложь;

    имяДокументаОснования = Неопределено;
    Параметры.Свойство("ИмяДокументаОснования", имяДокументаОснования);

    Если ТипЗнч(имяДокументаОснования) = Тип("Строка") И ЗначениеЗаполнено(имяДокументаОснования) Тогда
        Параметры.Свойство("Дата", Объект.ДатаПрайсЛиста);
        Параметры.Свойство("Контрагент", Объект.Поставщик);

        Если Объект.ДатаПрайсЛиста <> Неопределено Тогда
            Элементы.ДатаПрайсЛиста.Доступность = Ложь;
        КонецЕсли;

        Если Объект.Поставщик <> Неопределено Тогда
            Элементы.Поставщик.Доступность = Ложь;
        КонецЕсли;

        Элементы.ПоместитьВДокумент.Видимость = Истина;
    Иначе
        Параметры.Вставить("ИмяДокументаОснования", Неопределено);
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ЗагрузитьПрайсЛист(_)
    таблДок = Ждать получитьТабличныйДокументИзФайла();
    вывестиТабличныйДокументНаСервере(таблДок);
КонецПроцедуры

&НаСервере
Процедура вывестиТабличныйДокументНаСервере(Знач таблДок)
    Объект.ПрайсЛист.Вывести(таблДок);
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьВДокумент(_)
    имяСобытия = "ЗаписьПрайсЛистаВДокумент";
    параметрыОповещенияЗаписиПрайсЛиста = Новый Структура("Объект, ИмяДокументаОснования",
            ЭтотОбъект.Объект, ЭтотОбъект.Параметры.ИмяДокументаОснования);
    Оповестить(имяСобытия, параметрыОповещенияЗаписиПрайсЛиста, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Функция получитьТабличныйДокументИзФайла()
    результат = Неопределено;

    файлДанных = Ждать открытьТабличныйФайлДанных();
    Если файлДанных = Неопределено Тогда
        Возврат Неопределено; // Файл не выбран
    КонецЕсли;

    Попытка
        результат = получитьТабличныйДокументИзФайлаНаСервере(файлДанных.ИмяВременногоФайла);

    Исключение
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст = СтрШаблон(
                "Ошибка чтения фала прайс-листа: ""%1"".
                |Описание ошибки: %2",
                файлДанных.ОписаниеПомещенногоФайла.СсылкаНаФайл.Имя,
                ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
        сообщение.Сообщить();

        результат = Неопределено;
    КонецПопытки;

    удалитьВременныйФайлНаСервере(файлДанных.ИмяВременногоФайла);

    Возврат результат;
КонецФункции

&НаКлиенте
Асинх Функция открытьТабличныйФайлДанных()
    параметрыВыбораФайла = Новый ПараметрыДиалогаПомещенияФайлов(
            "Выбор файла прайс-листа", Ложь, "Файл данных (*.xls, *.xlsx, *.ods)|*.xls;*.xlsx;*.ods");
    описаниеФайлаДанных = Ждать ПоместитьФайлНаСерверАсинх( , , , параметрыВыбораФайла, ЭтотОбъект.УникальныйИдентификатор);

    Если описаниеФайлаДанных = Неопределено Тогда
        Возврат Неопределено; // Файл не выбран
    КонецЕсли;

    ДиагностикаКлиентСервер.Утверждение(описаниеФайлаДанных.ПомещениеФайлаОтменено = Ложь
            И описаниеФайлаДанных.Адрес <> Неопределено,
            "Файл должен быть помещен в хранилище.");

    имяФайлаДанных = Ждать записатьВременныйФайлНаСервере(описаниеФайлаДанных.Адрес, описаниеФайлаДанных.СсылкаНаФайл.Расширение);

    Возврат Новый Структура("ИмяВременногоФайла, ОписаниеПомещенногоФайла", имяФайлаДанных, описаниеФайлаДанных);
КонецФункции

&НаСервереБезКонтекста
Функция записатьВременныйФайлНаСервере(Знач адрес, Знач расширение)
    // BSLLS:MissingTemporaryFileDeletion-off
    имяФайлаДанных = ПолучитьИмяВременногоФайла(расширение);
    потокДанных = ПолучитьИзВременногоХранилища(адрес);

    ДиагностикаКлиентСервер.Утверждение(ТипЗнч(потокДанных) = Тип("ДвоичныеДанные"),
            "Данные полученные из временного хранилища должны быть типа: ""ДвоичныеДанные""");

    потокДанных.Записать(имяФайлаДанных);

    Возврат имяФайлаДанных;
КонецФункции

&НаСервереБезКонтекста
Функция получитьТабличныйДокументИзФайлаНаСервере(Знач имяФайлаДанных)
    результат = Новый ТабличныйДокумент;
    результат.Прочитать(имяФайлаДанных);

    Возврат результат;
КонецФункции

&НаСервереБезКонтекста
Функция удалитьВременныйФайлНаСервере(Знач имяФайлаДанных)
    Попытка
        УдалитьФайлы(имяФайлаДанных);
    Исключение
        записьОбОшибкеВЖурналРегистрацииНаСервере("ПолучитьТабличныйДокументИзФайла",
            ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        Возврат Ложь;
    КонецПопытки;

    Возврат Истина;
КонецФункции

&НаСервереБезКонтекста
Процедура записьОбОшибкеВЖурналРегистрацииНаСервере(Знач имяСобытия, Знач комментарий)
    ЗаписьЖурналаРегистрации(СтрШаблон("ЗагрузкаПрайсЛиста.%1", имяСобытия), УровеньЖурналаРегистрации.Ошибка, , ,
        ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
