#Область ПрограммныйИнтерфейс

// Параметры:
//	номенклатура - СправочникСсылка.Номенклатура
// 	видЦены	- Перечисления.ВидыЦенПродажи
//		- Неопределено
//	датаЦены - Дата
//		- Неопределено
//	наКонкретнуюДату - Булево - Если Ложь: поиск цены выполняется по СрезуПоследних, иначе поиск выполняется на конкретную дату
//
// Возвращаемое значение:
//	Число - значение цены
//	Неопределено - если значение цены для параметров отбора неопределено
//
Функция ПолучитьЦенуПродажиНаДату(номенклатура, Знач видЦены = Неопределено, Знач датаЦены = Неопределено, наКонкретнуюДату = Ложь) Экспорт
	видЦены = ?(видЦены = Неопределено, Перечисления.ВидыЦенПродажи.РозничнаяЦена, видЦены);

	Если наКонкретнуюДату Тогда
		Возврат ПолучитьЦенуНаКонкретнуюДату(номенклатура, видЦены, ?(датаЦены = Неопределено, ТекущаяДатаСеанса(), датаЦены));
	КонецЕсли;

	запросЦены = Новый Запрос;
	запросЦены.Текст = "
		| ВЫБРАТЬ
		| 	ЦеныНоменклатуры.Цена КАК Цена
		| ИЗ
		| 	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|		&Период,
		|		Номенклатура = &Номенклатура
		|			И ВидЦены = &ВидЦены
		|	) КАК ЦеныНоменклатуры
		|";

	запросЦены.УстановитьПараметр("Номенклатура", номенклатура);
	запросЦены.УстановитьПараметр("Период", датаЦены);
	запросЦены.УстановитьПараметр("ВидЦены", видЦены);

	результатЗапроса = запросЦены.Выполнить();
	Если результатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	выборкаЦен = результатЗапроса.Выбрать();
	выборкаЦен.Следующий();

	Возврат выборкаЦен.Цена;
КонецФункции

Функция ПолучитьЦенуНаКонкретнуюДату(номенклатура, Знач видЦены, Знач датаЦены) Экспорт

	запросЦены = Новый Запрос;
	запросЦены.Текст = "ВЫБРАТЬ
		|	ЦеныНоменклатуры.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|ГДЕ
		|	ЦеныНоменклатуры.Период = &Период
		|		И ЦеныНоменклатуры.Номенклатура = &Номенклатура
		|		И ЦеныНоменклатуры.ВидЦены = &ВидЦены";

	запросЦены.УстановитьПараметр("Номенклатура", номенклатура);
	запросЦены.УстановитьПараметр("Период", датаЦены);
	запросЦены.УстановитьПараметр("ВидЦены", видЦены);

	результатЗапроса = запросЦены.Выполнить();

	Если результатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	выборкаЦен = результатЗапроса.Выбрать();

	выборкаЦен.Следующий();

	Возврат выборкаЦен.Цена;

КонецФункции

// Параметры:
// 	номенклатура - СправочникСсылка.Номенклатура
//
// Возвращаемое значение:
// 	СправочникСсылка.СтатьиЗатрат
//
Функция ПолучитьСтатьюЗатратПоНоменклатуре(номенклатура) Экспорт
	запросСтатьяЗатрат = Новый Запрос;
	запросСтатьяЗатрат.УстановитьПараметр("Ссылка", номенклатура);

	запросСтатьяЗатрат.Текст =
		"ВЫБРАТЬ
		|	Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка = &Ссылка";

	выборка = запросСтатьяЗатрат.Выполнить().Выбрать();
	выборка.Следующий();

	Возврат выборка.СтатьяЗатрат;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
