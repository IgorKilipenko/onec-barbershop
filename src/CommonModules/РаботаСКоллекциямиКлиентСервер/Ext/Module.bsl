#Область ПрограммныйИнтерфейс

#Область ПроверкаТипов
// Параметры:
//  объект - Произвольный
//  строго - Булево - По умолчанию: Истина
// Возвращаемое значение:
//  - Булево
Функция ЭтоМассив(Знач объект, Знач строго = Истина) Экспорт
    типОбъекта = ТипЗнч(объект);
    Возврат типОбъекта = Тип("Массив") ИЛИ (НЕ строго И типОбъекта = Тип("ФиксированныйМассив"));
КонецФункции

// Параметры:
//  объект - Произвольный
//  строго - Булево - По умолчанию: Истина
// Возвращаемое значение:
//  - Булево
Функция ЭтоСтруктура(Знач объект, Знач строго = Истина) Экспорт
    типОбъекта = ТипЗнч(объект);
    Возврат типОбъекта = Тип("Структура") ИЛИ (НЕ строго И типОбъекта = Тип("ФиксированнаяСтруктура"));
КонецФункции

// Параметры:
//  объект - Произвольный
//  строго - Булево - По умолчанию: Истина
// Возвращаемое значение:
//  - Булево
Функция ЭтоСоответствие(Знач объект, Знач строго = Истина) Экспорт
    типОбъекта = ТипЗнч(объект);
    Возврат типОбъекта = Тип("Соответствие") ИЛИ (НЕ строго И типОбъекта = Тип("ФиксированноеСоответствие"));
КонецФункции

// Параметры:
//  объект - Произвольный
//  списокТипов - Массив, ФиксированныйМассив
// Возвращаемое значение:
//  - Булево
Функция ЭтоТипИзСписка(Знач объект, Знач списокТипов) Экспорт
    типОбъекта = ТипЗнч(объект);
    Возврат списокТипов.Найти(типОбъекта) <> Неопределено;
КонецФункции
#КонецОбласти // ПроверкаТипов

// Параметры:
//  объект - Структура, ФиксированнаяСтруктура
//  списокКлючей - Массив, ФиксированныйМассив, Строка, Структура, ФиксированнаяСтруктура
// Возвращаемое значение:
//  - Булево
Функция СтруктураСодержитКлючи(Знач объект, Знач списокКлючей) Экспорт
    ДиагностикаКлиентСервер.Утверждение(ЭтоСтруктура(объект, Ложь),
        "Аргумент ""Объект"" должен иметь тип значения: ""Структура"" | ""ФиксированнаяСтруктура"" | ""Строка"".");

    Если ТипЗнч(списокКлючей) = Тип("Строка") Тогда
        списокКлючей = Новый ФиксированныйМассив(СтрРазделить(списокКлючей, ",", Ложь));
    ИначеЕсли ЭтоСтруктура(списокКлючей, Ложь) Тогда
        списокКлючей = Новый ФиксированныйМассив(ПолучитьКлючиСтруктуры(списокКлючей));
    КонецЕсли;

    Для Каждого имяСвойства Из списокКлючей Цикл
        Если НЕ объект.Свойство(имяСвойства) Тогда
            Возврат Ложь;
        КонецЕсли;
    КонецЦикла;

    Возврат объект.Количество() > 0 И списокКлючей.Количество() > 0;
КонецФункции

// Параметры:
//  объект - Структура, ФиксированнаяСтруктура
// Возвращаемое значение:
//  - Массив
Функция ПолучитьКлючиСтруктуры(Знач объект) Экспорт
    ДиагностикаКлиентСервер.Утверждение(ЭтоСтруктура(объект, Ложь),
        "Аргумент ""Объект"" должен иметь тип значения: ""Структура"" | ""ФиксированнаяСтруктура"".");

    результат = Новый Массив(объект.Количество());
    сч = 0;
    Для Каждого элемент Из объект Цикл
        результат.Вставить(сч, элемент.Ключ);
        сч = сч + 1;
    КонецЦикла;

    Возврат результат;
КонецФункции

// Параметры:
//  объект - Структура, ФиксированнаяСтруктура
// Возвращаемое значение:
//  - Массив
Функция ПолучитьЗначенияСтруктуры(Знач объект) Экспорт
    ДиагностикаКлиентСервер.Утверждение(ЭтоСтруктура(объект, Ложь),
        "Аргумент ""Объект"" должен иметь тип значения: ""Структура"" | ""ФиксированнаяСтруктура"".");

    результат = Новый Массив(объект.Количество());
    сч = 0;
    Для Каждого элемент Из объект Цикл
        результат.Вставить(сч, элемент.Значение);
        сч = сч + 1;
    КонецЦикла;

    Возврат результат;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
