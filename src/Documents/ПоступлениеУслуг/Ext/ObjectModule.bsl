#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(_, __, ___)
	РаботаСДокументами.ЗаполнитьПолеАвторДокументаНаСервере(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаПроведения(_, __)
	Движения.ЦеныНоменклатурыПоставщиков.Записывать = Истина;
	Движения.УчетЗатрат.Записывать = Истина;

	выборкаИтогиУслуг = получитьВыборкуИтогиУслуги();

	Пока выборкаИтогиУслуг.Следующий() Цикл
		выполнитьДвижениеУчетЗатрат(выборкаИтогиУслуг);

		выборкаЦеныНоменклатуры = выборкаИтогиУслуг.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока выборкаЦеныНоменклатуры.Следующий() Цикл
			выполнитьДвижениеЦеныНоменклатурыПоставщиков(выборкаЦеныНоменклатуры);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область Движения
Процедура выполнитьДвижениеУчетЗатрат(данныеЗатрат)
	движение = Движения.УчетЗатрат.Добавить();
	ЗаполнитьЗначенияСвойств(движение, данныеЗатрат);
	движение.Период = Дата;
КонецПроцедуры

Процедура выполнитьДвижениеЦеныНоменклатурыПоставщиков(данныеЦенНоменклатуры)
	движение = Движения.ЦеныНоменклатурыПоставщиков.Добавить();
	движение.Период = Дата;
	движение.Номенклатура = данныеЦенНоменклатуры.Номенклатура;
	движение.Поставщик = Поставщик;
	движение.Цена = данныеЦенНоменклатуры.Цена;
КонецПроцедуры
#КонецОбласти // Движения

Функция получитьВыборкуИтогиУслуги()
	запросУслуг = Новый Запрос;
	запросУслуг.УстановитьПараметр("Ссылка", Ссылка);
	запросУслуг.Текст =
		"ВЫБРАТЬ
		|    ПоступлениеУслугУслуги.Ссылка КАК Регистратор,
		|    ПоступлениеУслуг.Дата КАК Период,
		|    ПоступлениеУслугУслуги.Номенклатура КАК Номенклатура,
		|    ПоступлениеУслугУслуги.Сумма КАК Сумма,
		|    ПоступлениеУслугУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
		|    ПоступлениеУслугУслуги.Цена КАК Цена,
		|    ПоступлениеУслуг.Поставщик КАК Поставщик
		|ИЗ
		|    Документ.ПоступлениеУслуг.Услуги КАК ПоступлениеУслугУслуги
		|        ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеУслуг КАК ПоступлениеУслуг
		|        ПО ПоступлениеУслугУслуги.Ссылка = ПоступлениеУслуг.Ссылка
		|ГДЕ
		|    ПоступлениеУслуг.Ссылка = &Ссылка
		|ИТОГИ
		|    СУММА(Сумма),
		|    МАКСИМУМ(Цена)
		|ПО
		|    СтатьяЗатрат,
		|    Номенклатура";

	выборкаИтоги = запросУслуг.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Возврат выборкаИтоги;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
