#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(_, __)
    выполнитьВсеДвижения();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область Движения
Процедура выполнитьВсеДвижения()
    Движения.Начисления.Записывать = Истина;

    выполнитьДвиженияПоВсемНачислениям();
    Движения.Начисления.Записать();
    Движения.Начисления.РассчитатьСуммуНачисления();

КонецПроцедуры

Процедура выполнитьДвиженияПоВсемНачислениям()
    Для Каждого строкаНачисления Из ЭтотОбъект.Начисления Цикл
        выполнитьДвижениеЗПНачисления(строкаНачисления.ВидРасчета, строкаНачисления.Сотрудник,
            строкаНачисления.ГрафикРаботы, строкаНачисления.ПоказательРасчета,
            строкаНачисления.ДатаНачала, строкаНачисления.ДатаОкончания);
    КонецЦикла;
КонецПроцедуры

Процедура выполнитьДвижениеЗПНачисления(Знач видРасчета, Знач сотрудник, Знач график,
        Знач показательРасчета, Знач датаНачала, Знач датаОкончания)

    движение = Движения.Начисления.Добавить();
    движение.ПериодРегистрации = ЭтотОбъект.ПериодНачисления;

    движение.Сторно = Ложь;
    движение.ВидРасчета = видРасчета;
    движение.ПериодДействияНачало = датаНачала;
    движение.ПериодДействияКонец = датаОкончания;
    движение.БазовыйПериодНачало = датаНачала;
    движение.БазовыйПериодКонец = датаОкончания;
    движение.Сотрудник = сотрудник;
    движение.ПоказательРасчета = показательРасчета;
    движение.График = график;
КонецПроцедуры
#КонецОбласти // Движения

#КонецОбласти // СлужебныеПроцедурыИФункции
