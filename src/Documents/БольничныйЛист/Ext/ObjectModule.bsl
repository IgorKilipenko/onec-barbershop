#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(_, __)
    выполнитьВсеДвижения();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область Движения
Процедура выполнитьВсеДвижения()
    Движения.Начисления.Записывать = Истина;
    результатДвижения = выполнитьДвиженияНачисленийПоПериодам();
    Если результатДвижения.Сообщение <> Неопределено Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст = результатДвижения.Текст;
        сообщение.Сообщить();
    КонецЕсли;
КонецПроцедуры

Процедура выполнитьДвижениеНачисления(Знач структураНачисления)
    РегистрыРасчета.Начисления.ЗаполнитьДвижениеНачисления(
        Движения.Начисления.Добавить(), ЭтотОбъект.ПериодНачисления, структураНачисления);
КонецПроцедуры

Функция выполнитьДвиженияНачисленийПоПериодам()
    ДиагностикаКлиентСервер.Утверждение(ЗначениеЗаполнено(ЭтотОбъект.Сотрудник),
        "Поле ""Сотрудник"" должно быть заполнено до начала выполнения проведения.");

    результатДвижения = Новый Структура("Отказ, Сообщение", Ложь, Неопределено);

    окладСотрудника = РегистрыСведений.КадроваяИсторияСотрудников.ПолучитьОкладСотрудника(
            ЭтотОбъект.Сотрудник, ЭтотОбъект.ДатаОкончания);
    Если окладСотрудника = Неопределено Тогда
        результатДвижения.Сообщение = СтрШаблон(
                "Оклад сотрудника: ""%1"" на период больничного не определен.", Строка(ЭтотОбъект.Сотрудник));

        окладСотрудника = 0;
    КонецЕсли;

    месяцНачисления = НачалоМесяца(ЭтотОбъект.ДатаНачала);
    месяцОкончания = НачалоМесяца(ЭтотОбъект.ДатаОкончания);

    Пока месяцНачисления <= месяцОкончания Цикл
        структураНачисления = РегистрыРасчета.Начисления.СоздатьПустуюСтруктуруНачисления();

        структураНачисления.ВидРасчета = ПланыВидовРасчета.Начисления.Больничный;
        структураНачисления.ПериодДействияНачало = Макс(ЭтотОбъект.ДатаНачала, месяцНачисления);
        структураНачисления.ПериодДействияКонец = Мин(ЭтотОбъект.ДатаОкончания, КонецМесяца(месяцНачисления));
        структураНачисления.БазовыйПериодНачало = Макс(ЭтотОбъект.ДатаНачала, месяцНачисления);
        структураНачисления.БазовыйПериодКонец = Мин(ЭтотОбъект.ДатаОкончания, КонецМесяца(месяцНачисления));

        структураНачисления.Сотрудник = ЭтотОбъект.Сотрудник;
        структураНачисления.ПоказательРасчета = окладСотрудника;
        структураНачисления.Сумма = 0;

        выполнитьДвижениеНачисления(структураНачисления);

        месяцНачисления = ДобавитьМесяц(месяцНачисления, 1);
    КонецЦикла;

    Возврат результатДвижения;
КонецФункции
#КонецОбласти // Движения

#КонецОбласти // СлужебныеПроцедурыИФункции
