#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    ЭтотОбъект._Состояние = Новый Структура("АктивнаяФормаПрайсЛиста");
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(имяСобытия, параметрыСобытия, ___)
    Если имяСобытия <> "ЗаписьПрайсЛистаВДокумент" ИЛИ параметрыСобытия = Неопределено Тогда
        Возврат;
    КонецЕсли;

    создательФормы = Неопределено;
    загрузчик = Неопределено;

    параметрыСобытия.Свойство("СоздательФормы", создательФормы);
    Если создательФормы <> ЭтотОбъект.УникальныйИдентификатор Тогда
        Возврат;
    КонецЕсли;

    загрузчик = Неопределено;
    параметрыСобытия.Свойство("Объект", загрузчик);

    ДиагностикаКлиентСервер.Утверждение(загрузчик <> Неопределено);
    ДиагностикаКлиентСервер.Утверждение(загрузчик.ДатаПрайсЛиста <> Неопределено);
    ДиагностикаКлиентСервер.Утверждение(загрузчик.ПрайсЛист <> Неопределено);
    ДиагностикаКлиентСервер.Утверждение(загрузчик.Поставщик <> Неопределено);

    документУстановкиЦенСсылка = заполнитьДокументНаСервере(
            загрузчик.ДатаПрайсЛиста, загрузчик.Поставщик, загрузчик.ПрайсЛист);
    Если документУстановкиЦенСсылка.Пустая() Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст =
            "В документе отсутствуют данные цен номенклатуры.
            |Создание документа отменено.";
        сообщение.Сообщить();

        Возврат;
    КонецЕсли;

    Если ЭтотОбъект._Состояние.АктивнаяФормаПрайсЛиста <> Неопределено Тогда
        ЭтотОбъект._Состояние.АктивнаяФормаПрайсЛиста.Закрыть();
        ЭтотОбъект._Состояние.АктивнаяФормаПрайсЛиста = Неопределено;
    КонецЕсли;

    ОткрытьФорму(получитьПолноеИмяФормыДокументаНаСервере(документУстановкиЦенСсылка),
        Новый Структура("Ключ, СообщениеПриСоздании", документУстановкиЦенСсылка,
        "Документ создан по данным из прайс-листа."));
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьИзПрайсЛиста(_)
    форма = ФормыКонфигурацииКлиент.ОткрытьФормуЗагрузкиПрайсЛиста(ЭтотОбъект,
            Новый Структура("СоздательФормы", ЭтотОбъект.УникальныйИдентификатор));
    ЭтотОбъект._Состояние.АктивнаяФормаПрайсЛиста = форма;
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция заполнитьДокументНаСервере(Знач дата, Знач контрагент, Знач прайсЛист)
    документУстановкиЦен = Документы.УстановкаЦенПоставщика.СоздатьДокумент();

    документУстановкиЦен.Дата = дата;
    документУстановкиЦен.Контрагент = контрагент;
    Документы.УстановкаЦенПоставщика.ЗаполнитьТаблицуТоваров(документУстановкиЦен.Товары, прайсЛист);

    Если документУстановкиЦен.Товары.Количество() > 0 Тогда
        документУстановкиЦен.АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
        документУстановкиЦен.Записать();
    КонецЕсли;

    Возврат документУстановкиЦен.Ссылка;
КонецФункции

&НаСервереБезКонтекста
Функция получитьПолноеИмяФормыДокументаНаСервере(Знач документСсылка = Неопределено)
    Если документСсылка = Неопределено Тогда
        Возврат Метаданные.Документы.УстановкаЦенПоставщика.Формы.ФормаДокумента.ПолноеИмя();
    КонецЕсли;

    метаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(документСсылка));
    Если метаданныеОбъекта = Неопределено Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат метаданныеОбъекта.Формы.ФормаДокумента.ПолноеИмя();
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
