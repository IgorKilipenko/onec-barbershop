#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(_)
    ценаНоменклатурыОбновлена = обновитьЦенуНоменклатуры(Элементы.Товары.ТекущиеДанные);
    Если ценаНоменклатурыОбновлена Тогда
        обновитьСуммуПоСтрокеТовары();
        обновитьСуммуДокумента();
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(_)
    обновитьСуммуПоСтрокеТовары();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(_)
    обновитьСуммуПоСтрокеТовары();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(_)
    обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыТовары

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьЦены(_)
    списокНоменклатуры = РаботаСФормамиКлиент.ПолучитьСписокНоменклатуры(Объект.Товары);

    структураЦенНоменклатуры = получитьЦеныДляСпискаНоменклатурыНаСервере(
            списокНоменклатуры,
            Объект.Поставщик,
            ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, Неопределено));

    Если структураЦенНоменклатуры = Неопределено Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст =
            "Цены товаров не установлены.
            |Для текущего списка номенклатуры отсутствуют данные в регистре цен номенклатуры поставщиков";
        сообщение.Сообщить();

        Возврат;
    КонецЕсли;

    количествоОбновленных = РаботаСФормамиКлиент.ОбновитьЦеныНоменклатурыТаблицы(
            Объект.Товары, структураЦенНоменклатуры);

    сообщение = Новый СообщениеПользователю;
    сообщение.Текст = СтрШаблон(
            "Цены номенклатуры обновлены. Количество обновленных строк: ""%1"".", количествоОбновленных);
    сообщение.Сообщить();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура обновитьСуммуПоСтрокеТовары()
    текущСтрокаТовары = Элементы.Товары.ТекущиеДанные;
    текущСтрокаТовары.Сумма = текущСтрокаТовары.Цена * текущСтрокаТовары.Количество;
КонецПроцедуры

// Пересчитывает сумму документа
&НаКлиенте
Процедура обновитьСуммуДокумента()
    Объект.СуммаДокумента = Объект.Товары.Итог("Сумма");
КонецПроцедуры

// Параметры:
//  номенклатураСсылка - ДокументСсылка.Номенклатура
// Возвращаемое значение:
//  - ПеречислениеСсылка.ТипыНоменклатуры
&НаСервереБезКонтекста
Функция получитьТипНоменклатурыНаСервере(Знач номенклатураСсылка)
    результат = Справочники.Номенклатура.ПолучитьЗначениеТипаНоменклатуры(номенклатураСсылка);
    ДиагностикаКлиентСервер.Утверждение(результат <> Неопределено,
        "Ошибка при получении типа номенклатуры.");

    Возврат результат;
КонецФункции

// Параметры:
//  текущСтрокаТовары - СтрокаТабличнойЧасти
// Возвращаемое значение:
//  - Булево - Истина, если цена номенклатуры обновлена, если получить цену не удалось
//      или полученная цена не отличается от текущей возвращает - Ложь
&НаКлиенте
Функция обновитьЦенуНоменклатуры(Знач текущСтрокаТовары)
    ценаНоменклатуры = текущСтрокаТовары.Цена;

    Если ЗначениеЗаполнено(текущСтрокаТовары.Номенклатура)
        И ЗначениеЗаполнено(Объект.Поставщик) Тогда

        ценаНоменклатуры = получитьЦенуНоменклатурыНаСервере(
                текущСтрокаТовары.Номенклатура, Объект.Поставщик,
                ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, Неопределено));

        Если ценаНоменклатуры = 0 Тогда
            сообщение = Новый СообщениеПользователю;
            сообщение.Текст = СтрШаблон("Не удалось получить цену номенклатуры: ""%1"".", Строка(текущСтрокаТовары.Номенклатура));
            сообщение.Сообщить();
        КонецЕсли;
    КонецЕсли;

    Если текущСтрокаТовары.Цена = ценаНоменклатуры Тогда
        Возврат Ложь; // Без изменения
    КонецЕсли;

    текущСтрокаТовары.Цена = ценаНоменклатуры;
    Возврат Истина;
КонецФункции

// Параметры:
//  номенклатураСсылка - СправочникСсылка.Номенклатура
//  поставщикСсылка - СправочникСсылка.Контрагенты
//  дата - Дата
// Возвращаемое значение:
//  - Число
&НаСервереБезКонтекста
Функция получитьЦенуНоменклатурыНаСервере(Знач номенклатураСсылка, Знач поставщикСсылка, Знач дата)
    результат = РегистрыСведений.ЦеныНоменклатурыПоставщиков.ПолучитьЦенуНоменклатуры(
            номенклатураСсылка, поставщикСсылка, дата);

    Возврат ?(результат = Неопределено, 0, результат);
КонецФункции

// Параметры:
//  списокНоменклатуры - Массив, ФиксированныйМассив Из ДокументСсылка.Номенклатура
//  поставщикСсылка - ДокументСсылка.Контрагенты
//  дата - Дата, МоментВремени, Граница, Неопределено
// Возвращаемое значение:
//  - Соответствие - Таблица цен номенклатуры поставщика
//      * Ключ - ДокументСсылка.Номенклатура
//      * Значение - Число
//  - Неопределено - Если данные отсутствуют
&НаСервереБезКонтекста
Функция получитьЦеныДляСпискаНоменклатурыНаСервере(Знач списокНоменклатуры, Знач поставщикСсылка, Знач дата)
    таблицаЦенНоменклатуры = РегистрыСведений.ЦеныНоменклатурыПоставщиков.ПолучитьСписокЦенНоменклатуры(
            списокНоменклатуры, поставщикСсылка, дата);

    Если таблицаЦенНоменклатуры = Неопределено Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат РегистрыСведений.ЦеныНоменклатурыПоставщиков.КонвертироватьТаблицуЦенНоменклатурыВСоответствие(таблицаЦенНоменклатуры);
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
