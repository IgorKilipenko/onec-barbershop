#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьЦены(_)
    списокНоменклатуры = Новый Массив;
    Для Каждого строкаНоменклатуры Из Объект.Товары Цикл
        Если НЕ ЗначениеЗаполнено(строкаНоменклатуры.Номенклатура) Тогда
            Продолжить;
        КонецЕсли;

        списокНоменклатуры.Добавить(строкаНоменклатуры.Номенклатура);
    КонецЦикла;

    таблицаЦенНоменклатуры = получитьЦеныДляСпискаНоменклатурыНаСервере(
            списокНоменклатуры,
            Объект.Поставщик,
            ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, Неопределено));

    Если таблицаЦенНоменклатуры = Неопределено Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст =
            "Цены товаров не установлены.
            |Для текущего списка номенклатуры отсутствуют данные в регистре цен номенклатуры поставщиков";
        сообщение.Сообщить();

        Возврат;
    КонецЕсли;

    количествоОбновленных = 0;
    Для Каждого строкаНоменклатуры Из Объект.Товары Цикл
        Если НЕ ЗначениеЗаполнено(строкаНоменклатуры.Номенклатура) Тогда
            Продолжить;
        КонецЕсли;

        ценаНоменклатуры = таблицаЦенНоменклатуры.Получить(строкаНоменклатуры.Номенклатура);
        Если ценаНоменклатуры = Неопределено Тогда
            сообщение = Новый СообщениеПользователю;
            сообщение.Текст = СтрШаблон("Цена поставщика для номенклатуры ""%1"" не установлена. Номер строки - ""%2"".",
                    Строка(строкаНоменклатуры.Номенклатура), строкаНоменклатуры.НомерСтроки);
            сообщение.Сообщить();
        КонецЕсли;

        строкаНоменклатуры.Цена = ?(ценаНоменклатуры = Неопределено, 0, ценаНоменклатуры);
        Если ценаНоменклатуры <> Неопределено И ценаНоменклатуры <> строкаНоменклатуры.Цена Тогда
            количествоОбновленных = количествоОбновленных + 1;
            строкаНоменклатуры.Цена = ценаНоменклатуры;
        КонецЕсли;
    КонецЦикла;

    сообщение = Новый СообщениеПользователю;
    сообщение.Текст = СтрШаблон(
            "Цены номенклатуры обновлены. Количество обновленных строк: ""%1"".", количествоОбновленных);
    сообщение.Сообщить();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(_)
    рассчитатьСуммуПоСтрокеТовары();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(_)
    рассчитатьСуммуПоСтрокеТовары();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(_)
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(_)
    Если обновитьЦенуНоменклатуры(Элементы.Товары.ТекущиеДанные) Тогда
        рассчитатьСуммуПоСтрокеТовары();
        обновитьСуммуДокумента();
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикПриИзменении(_)
    ТоварыНоменклатураПриИзменении(Неопределено);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыТовары

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура рассчитатьСуммуПоСтрокеТовары()
    текущСтрокаТовары = Элементы.Товары.ТекущиеДанные;
    текущСтрокаТовары.Сумма = текущСтрокаТовары.Цена * текущСтрокаТовары.Количество;
КонецПроцедуры

&НаКлиенте
Процедура обновитьСуммуДокумента()
    Объект.СуммаДокумента = Объект.Товары.Итог("Сумма");
КонецПроцедуры

&НаКлиенте
Функция обновитьЦенуНоменклатуры(Знач текущСтрокаТовары)
    ценаНоменклатуры = текущСтрокаТовары.Цена;

    Если ЗначениеЗаполнено(текущСтрокаТовары.Номенклатура)
        И ЗначениеЗаполнено(Объект.Поставщик) Тогда

        ценаНоменклатуры = получитьЦенуНоменклатурыНаСервере(
                текущСтрокаТовары.Номенклатура, Объект.Поставщик,
                ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, Неопределено));
    КонецЕсли;

    Если текущСтрокаТовары.Цена = ценаНоменклатуры Тогда
        Возврат Ложь; // Без изменения
    КонецЕсли;

    текущСтрокаТовары.Цена = ценаНоменклатуры;
    Возврат Истина;
КонецФункции

&НаСервереБезКонтекста
Функция получитьЦенуНоменклатурыНаСервере(Знач номенклатураСсылка, Знач поставщикСсылка, Знач дата)
    результат = РегистрыСведений.ЦеныНоменклатурыПоставщиков.ПолучитьЦенуНоменклатуры(
            номенклатураСсылка, поставщикСсылка, дата);

    Возврат ?(результат = Неопределено, 0, результат);
КонецФункции

// Параметры:
//  списокНоменклатуры - Массив, ФиксированныйМассив Из ДокументСсылка.Номенклатура
//  поставщикСсылка - ДокументСсылка.Контрагенты
//  дата - Дата, МоментВремени, Граница, Неопределено
// Возвращаемое значение:
//  - ФиксированноеСоответствие - Таблица цен номенклатуры поставщика
//      * Ключ - ДокументСсылка.Номенклатура
//      * Значение - Число
//  - Неопределено - Если данные отсутствуют
&НаСервереБезКонтекста
Функция получитьЦеныДляСпискаНоменклатурыНаСервере(Знач списокНоменклатуры, Знач поставщикСсылка, Знач дата)
    таблицаЦенНоменклатуры = РегистрыСведений.ЦеныНоменклатурыПоставщиков.ПолучитьСписокЦенНоменклатуры(
            списокНоменклатуры, поставщикСсылка, дата);

    Если таблицаЦенНоменклатуры = Неопределено Тогда
        Возврат Неопределено;
    КонецЕсли;

    имяКолонкиНоменклатура = "Номенклатура";
    имяКолонкиЦена = "Цена";
    колонкаНоменклатура = таблицаЦенНоменклатуры.Колонки.Найти(имяКолонкиНоменклатура);
    колонкаЦена = таблицаЦенНоменклатуры.Колонки.Найти(имяКолонкиЦена);

    ДиагностикаКлиентСервер.Утверждение(колонкаЦена <> Неопределено И колонкаНоменклатура <> Неопределено,
        СтрШаблон("Таблица значений цен номенклатуры должна содержать обязательные колонки: [%1, %2].",
            имяКолонкиНоменклатура, имяКолонкиЦена));

    индексКолонкиНоменклатура = таблицаЦенНоменклатуры.Колонки.Индекс(колонкаНоменклатура);
    индексКолонкиЦена = таблицаЦенНоменклатуры.Колонки.Индекс(колонкаЦена);

    результат = Новый Соответствие;
    Для Каждого строкаЦенНоменклатуры Из таблицаЦенНоменклатуры Цикл
        результат.Вставить(строкаЦенНоменклатуры.Получить(индексКолонкиНоменклатура),
            строкаЦенНоменклатуры.Получить(индексКолонкиЦена));
    КонецЦикла;

    Возврат Новый ФиксированноеСоответствие(результат);
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
