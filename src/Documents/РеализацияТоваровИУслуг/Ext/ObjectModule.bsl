#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(_, __)
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ЗаказыКлиентов.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;

	Для Каждого текСтрокаТовары Из Товары Цикл
		выполнитьДвижениеТоварыНаСкладахРасход(текСтрокаТовары);
		выполнитьДвижениеПродажиОборот(текСтрокаТовары);
	КонецЦикла;

	Для Каждого текСтрокаУслуги Из Услуги Цикл
		выполнитьДвижениеЗаказыКлиентовРасход(текСтрокаУслуги);
		выполнитьДвижениеПродажиОборот(текСтрокаУслуги);
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаЗаполнения(данныеЗаполнения, __)
	Если ТипЗнч(данныеЗаполнения) = Тип("ДокументСсылка.ЗаписьКлиента") Тогда
		АвторДокумента = данныеЗаполнения.АвторДокумента;
		ДатаОказанияУслуги = данныеЗаполнения.ДатаЗаписи;
		Клиент = данныеЗаполнения.Клиент;
		Сотрудник = данныеЗаполнения.Сотрудник;
		ДокументОснование = данныеЗаполнения;

		Для Каждого текСтрокаУслуги Из данныеЗаполнения.Услуги Цикл
			новаяСтрока = Услуги.Добавить();
			новаяСтрока.Количество = текСтрокаУслуги.Количество;
			новаяСтрока.Номенклатура = текСтрокаУслуги.Номенклатура;
			новаяСтрока.Сумма = текСтрокаУслуги.Сумма;
			новаяСтрока.Цена = текСтрокаУслуги.Цена;
		КонецЦикла;

	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(_, __, ___)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	АвторДокумента = ?(ЗначениеЗаполнено(АвторДокумента), АвторДокумента, ПараметрыСеанса.ТекущийПользователь);
	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область Движения
Процедура выполнитьДвижениеТоварыНаСкладахРасход(текСтрокаТовары)
	движение = Движения.ТоварыНаСкладах.Добавить();
	движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	движение.Период = Дата;
	движение.Номенклатура = текСтрокаТовары.Номенклатура;
	движение.Склад = текСтрокаТовары.Склад;
	движение.Количество = текСтрокаТовары.Количество;
КонецПроцедуры

Процедура выполнитьДвижениеЗаказыКлиентовРасход(текСтрокаУслуги)
	движение = Движения.ЗаказыКлиентов.Добавить();
	движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	движение.Период = Дата;
	движение.Клиент = Клиент;
	движение.ДатаЗаписи = ДатаОказанияУслуги;
	движение.Количество = текСтрокаУслуги.Количество;
	движение.Сумма = текСтрокаУслуги.Сумма;
КонецПроцедуры

Процедура выполнитьДвижениеПродажиОборот(текСтрока)
	движение = Движения.Продажи.Добавить();
	движение.Период = Дата;
	движение.Номенклатура = текСтрока.Номенклатура;
	движение.Сотрудник = Сотрудник;
	движение.Количество = текСтрока.Количество;
	движение.Сумма = текСтрока.Сумма;
КонецПроцедуры
#КонецОбласти // Движения

#КонецОбласти // СлужебныеПроцедурыИФункции
