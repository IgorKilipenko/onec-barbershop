#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(_, __)
    Объект.ПризнакОплаты = получитьСтатусОплатыДокумента(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(_)
    Если получитьЭтоРеализацияНаОснованииЗаписиКлиента() Тогда
        Элементы.Сотрудник.ТолькоПросмотр = Истина;
        Элементы.Клиент.ТолькоПросмотр = Истина;
        Элементы.ДатаОказанияУслуги.ТолькоПросмотр = Истина;
        Элементы.ДатаОказанияУслуги.Вид = ВидПоляФормы.ПолеНадписи;
        Элементы.Товары.Доступность = Ложь;
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(_)
    рассчитатьСуммуПоСтрокеТовары();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(_)
    рассчитатьСуммуПоСтрокеТовары();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(_)
    строкаТЧ = Элементы.Товары.ТекущиеДанные;
    строкаТЧ.Цена = получитьЦенуПродажи(строкаТЧ.Номенклатура);

    рассчитатьСуммуПоСтрокеТовары();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(_)
    обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыТовары

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиЦенаПриИзменении(_)
    рассчитатьСуммуПоСтрокеУслуги();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(_)
    рассчитатьСуммуПоСтрокеУслуги();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(_)
    строкаТЧ = Элементы.Услуги.ТекущиеДанные;
    строкаТЧ.Цена = получитьЦенуПродажи(строкаТЧ.Номенклатура);

    рассчитатьСуммуПоСтрокеУслуги();
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиПослеУдаления(_)
    обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыУслуги

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция получитьСтатусОплатыДокумента(Знач документРТУСсылка)
    Возврат Документы.РеализацияТоваровИУслуг.ПроверитьСтатусОплаты(документРТУСсылка, Неопределено).ПризнакОплаты;
КонецФункции

&НаКлиенте
Процедура рассчитатьСуммуПоСтрокеУслуги()
    текущСтрокаУслуги = Элементы.Услуги.ТекущиеДанные;
    текущСтрокаУслуги.Сумма = текущСтрокаУслуги.Цена * текущСтрокаУслуги.Количество;
КонецПроцедуры

&НаКлиенте
Процедура рассчитатьСуммуПоСтрокеТовары()
    текущСтрокаТовары = Элементы.Товары.ТекущиеДанные;
    текущСтрокаТовары.Сумма = текущСтрокаТовары.Цена * текущСтрокаТовары.Количество;
КонецПроцедуры

&НаКлиенте
Процедура обновитьСуммуДокумента()
    Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
КонецПроцедуры

&НаКлиенте
Функция получитьЦенуПродажи(номенклатура)
    Возврат ?(ЗначениеЗаполнено(номенклатура),
        РаботаСЦенамиВызовСервера.ПолучитьЦенуПродажиНаДату(номенклатура), 0);
КонецФункции

&НаКлиенте
Функция получитьЭтоРеализацияНаОснованииЗаписиКлиента()
    Возврат ЗначениеЗаполнено(Объект.ДокументОснование) И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ЗаписьКлиента");
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
